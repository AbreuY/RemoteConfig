apply from: '../dependencies.gradle'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'RemoteConfig'

    publishedGroupId = 'me.giacoppo'
    libraryName = 'RemoteConfig'
    artifact = 'remoteconfig'

    libraryDescription = 'RemoteConfig is an Android library that lets you manage all your remote configuration without requiring developers to manually download each configuration and integrate them into the application.'

    siteUrl = 'https://github.com/GiuseppeGiacoppo/RemoteConfig'
    gitUrl = 'https://github.com/GiuseppeGiacoppo/RemoteConfig.git'

    libraryVersion = versionName

    developerId = 'giuseppegiacoppo'
    developerName = 'Giuseppe Giacoppo'
    developerEmail = 'giuseppegiacoppo@hotmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

// scripts based on configured variables

group = publishedGroupId
version = versionName

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    override = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        description = libraryDescription
        publish = true
        publicDownloadNumbers = true
        licenses = allLicenses
        vcsUrl = gitUrl
        version {
            desc = libraryDescription
            released = new Date()
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}